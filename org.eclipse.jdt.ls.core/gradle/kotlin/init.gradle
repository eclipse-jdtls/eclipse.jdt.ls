import org.gradle.plugins.ide.eclipse.model.EclipseModel

gradle.projectsEvaluated { g ->
	if (System.getProperty("eclipse.application") == null) return
	def allprojects = g.rootProject.allprojects
	allprojects.each { p ->
		if (p.plugins.hasPlugin('eclipse') && (p.plugins.hasPlugin('org.jetbrains.kotlin.jvm') || p.plugins.hasPlugin('kotlin'))) {
			EclipseModel eclipseModel = (EclipseModel) p.property("eclipse")
			String[] taskNames = [ "compileKotlin", "compileTestKotlin" ]
			eclipseModel.classpath.file.whenMerged { cp ->
				File projectDir = p.getProjectDir()
				for (final String taskName : taskNames) {
					Task task
					try {
						task = p.getTasks().getByName(taskName)
					} catch (UnknownTaskException e) {
						task = null;
					}
					if (task == null) {
						continue
					}
					if (task.hasProperty("destinationDirectory")) {
						def prop = task.destinationDirectory;
						Object outputDir = null
						if (prop instanceof org.gradle.api.file.DirectoryProperty) {
							outputDir = prop.asFile.getOrNull()
						} else if (prop instanceof File) {
							outputDir = prop
						}
						if (outputDir != null) {
							outputDir.mkdirs()
							cp.entries.removeAll { it instanceof org.gradle.plugins.ide.eclipse.model.Library && it.path.equals(outputDir.toString()) }
							Object entry = new org.gradle.plugins.ide.eclipse.model.Library(fileReference(outputDir))
							entry.exported = true
							entry.entryAttributes.put("kotlin_generated", "true")
							entry.entryAttributes.put("optional", "true")
							entry.entryAttributes.put("ignore_optional_problems", "true")
							if (taskName == 'compileTestKotlin') {
								entry.entryAttributes.put("test", "true")
							}
							cp.entries.add(entry)
						}
					}
				}
			}
		}
	}
}

